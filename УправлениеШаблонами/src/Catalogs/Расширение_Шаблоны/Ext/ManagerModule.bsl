#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Дополнительный реквизит параметр
//
// Возвращаемое значение:
//  Строка - ключ
//
Функция ДополнительныйРеквизитПараметр() Экспорт
	Возврат "ОперационныйМенеджерКлиента_266aa9d845c0482bbfd505d6034053f6";
КонецФункции

// Функция - Получить параметры текста сообщения
//
// Параметры:
//  ТекстСообщения	 - Строка	 - шаблон сообщения
//  Контрагент		 - СправочникСсылка.Контрагенты	 -
//
// Возвращаемое значение:
//  Соответствие - Ключ:Значение
//
Функция ПолучитьПараметрыТекстаСообщения(Знач ТекстСообщения, Знач Контрагент) Экспорт
	
	МассивПараметров = Новый Массив;
	
	Текст = ТекстСообщения;
	Позиция = СтрНайти(Текст, "[");
	Пока Позиция > 0 Цикл
		Текст = Сред(Текст, Позиция + 1);
		Позиция = СтрНайти(Текст, "]");
		Если Позиция > 0 Тогда
			НайденныйПараметр = Лев(Текст, Позиция - 1);
			Если МассивПараметров.Найти(НайденныйПараметр) = Неопределено Тогда
				МассивПараметров.Добавить(НайденныйПараметр);
			КонецЕсли;
			Текст = Сред(Текст, Позиция + 1);
		КонецЕсли;
		Позиция = СтрНайти(Текст, "[");
	КонецЦикла;
	
	СоответствиеПараметров = Новый Соответствие;
	
	СвойстваОбъекта = УправлениеСвойствами.СвойстваОбъекта(Контрагент, Истина, Истина);
	
	Для каждого ДополнительныйРеквизит Из СвойстваОбъекта Цикл
		
		Если НайденоНеобходимоеСвойство(ДополнительныйРеквизит, МассивПараметров) Тогда
			
			// todo: странное место, вроде как нужно получить не значение реквизита, а именно наименование контрагента
			// 05.03.2021, если будет ошибка, тогда раскомментировать этот блок.
			// СоответствиеПараметров.Вставить(ДополнительныйРеквизит.Имя, Строка(Контрагент));
			
			СоответствиеПараметров.Вставить(ДополнительныйРеквизит.Имя,
				Строка(УправлениеСвойствами.ЗначениеСвойства(Контрагент, ДополнительныйРеквизит)));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СоответствиеПараметров;
	
КонецФункции

// Функция - Получить заполненный текст сообщения
//
// Параметры:
//  ТекстШаблона - Строка						 - текст шаблона
//  Контрагент	 - СправочникСсылка.Контрагенты	 - ссылка на справочник
//
// Возвращаемое значение:
//  ТекстШаблона - Строка
//
Функция ПолучитьЗаполненныйТекстСообщения(Знач ТекстШаблона, Знач Контрагент) Экспорт
	
	ПараметрыТекстаСообщения = ПолучитьПараметрыТекстаСообщения(ТекстШаблона, Контрагент);
	
	Для каждого КлючИЗначение Из ПараметрыТекстаСообщения Цикл
		ТекстШаблона = СтрЗаменить(ТекстШаблона, СтрШаблон("[%1]", КлючИЗначение.Ключ), КлючИЗначение.Значение);
	КонецЦикла;
	
	Возврат ТекстШаблона;
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НайденоНеобходимоеСвойство(Знач ДополнительныйРеквизит, МассивПараметров)
	
	Возврат Не ДополнительныйРеквизит.ПометкаУдаления
				И ДополнительныйРеквизит.Виден
				И ДополнительныйРеквизит.Доступен
				И Не МассивПараметров.Найти(ДополнительныйРеквизит.Имя) = Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли